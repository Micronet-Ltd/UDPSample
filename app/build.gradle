apply plugin: 'com.android.application'

android {

    signingConfigs{
        obc5keys
        tab8keys
    }


    compileSdkVersion 29
    buildToolsVersion "29.0.2"
    defaultConfig {
        applicationId "com.micronet.udpsampleappv2"
        minSdkVersion 15
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            versionNameSuffix "-debug"
            signingConfig null // trick to eliminate default signing config for debug so it can be specified in flavors
        }
    }
    flavorDimensions "device"
    productFlavors {
        tab8 {
            dimension "device"
            applicationIdSuffix ""
            versionNameSuffix "-tab8"
            buildConfigField "String", "BUILD_DEVICE", '"tab8"'
            minSdkVersion 15
            targetSdkVersion 28
            signingConfig signingConfigs.tab8keys
        }
        obc5 {
            dimension "device"
            //applicationIdSuffix ".obc5"
            applicationIdSuffix "" // leave this blank because ATS uses the application name to start VBS
            versionNameSuffix "-obc5"
            buildConfigField "String", "BUILD_DEVICE", '"obc5"'
            minSdkVersion 15
            targetSdkVersion 28
            signingConfig signingConfigs.obc5keys
            dependencies {
                implementation fileTree(dir: 'obc5/jniLibs', include: ['.so'])
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.0.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'
}

def obc5_props = new Properties()
obc5_props.load(new FileInputStream(rootProject.file("obc5keys.properties")))
android.signingConfigs.obc5keys.storeFile rootProject.file(obc5_props.keyStore)
android.signingConfigs.obc5keys.storePassword obc5_props.keyStorePassword
android.signingConfigs.obc5keys.keyAlias obc5_props.keyAlias
android.signingConfigs.obc5keys.keyPassword obc5_props.keyAliasPassword

def tab8_props = new Properties()
tab8_props.load(new FileInputStream(rootProject.file("tab8platformkey.properties")))
android.signingConfigs.tab8keys.storeFile rootProject.file(tab8_props.keyStore)
android.signingConfigs.tab8keys.storePassword tab8_props.keyStorePassword
android.signingConfigs.tab8keys.keyAlias tab8_props.keyAlias
android.signingConfigs.tab8keys.keyPassword tab8_props.keyAliasPassword